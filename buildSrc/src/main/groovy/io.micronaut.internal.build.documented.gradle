import org.asciidoctor.gradle.jvm.AsciidoctorTask

plugins {
    id "org.asciidoctor.jvm.convert"
}

repositories {
    mavenCentral()
}

asciidoctorj {
    attributes 'source-highlighter': 'highlight.js',
            'highlightjsdir': 'highlight',
            'toc': 'left',
            'reproducible': '',
            'nofooter': '',
            'sectnums': '',
            'sectanchors': ''
}

def asciidoctor = tasks.named("asciidoctor", AsciidoctorTask)

asciidoctor.configure {
    outputDir = layout.buildDirectory.dir("asciidoc").get().asFile
    resources {
        from("src/docs/asciidoc/highlight") {
            into("highlight")
        }
        from("src/docs/asciidoc/css") {
            into("css")
        }
        from("src/docs/asciidoc/js") {
            into("js")
        }
    }
}

def themer = tasks.register("asciidocThemer", io.micronaut.internal.build.docs.Themer) {
    template = layout.projectDirectory.file("src/docs/asciidoc/template/template.html")
    asciidocHtml.fileProvider(asciidoctor.map {
        it.outputDir
    })
    outputDirectory = layout.buildDirectory.dir("themed")
}

configurations {
    javadoc {
        canBeResolved = true
        canBeConsumed = false
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, LibraryElements.JAR))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
        }
    }
}

def javadoc = tasks.register("javadoc", Javadoc) {
    description = "Generate javadocs from all plugins"
    group = "Documentation"
    destinationDir = layout.buildDirectory.dir("javadoc").get().asFile
    title = "Micronaut Gradle plugins API"
    options.author = true
    source = subprojects.findAll {
        it.name.matches(".*-plugin")
    }.collect {
        it.sourceSets.main.java.srcDirs
    }
    classpath = configurations.javadoc
}

tasks.register("docs", Copy) {
    description = "Generates the plugin documentation"
    destinationDir = layout.buildDirectory.dir("docs").get().asFile
    from(themer)
    into("api") {
        from javadoc
    }
}
